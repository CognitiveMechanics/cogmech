

@include "../lib/std.cogm"
@include "../lib/math.cogm"


// ------------------------------------------------------------------------------------------------------------------ //
// SPECIFIERS
// ------------------------------------------------------------------------------------------------------------------ //


// This is the specifier for states of the machine.

universal (name, structure, configurations, index, next, matched, instruction, result) ->
  <
    <"structure"    , structure>,
    <"configurations", configurations>,
    <"index"        , index>,
    <"next"         , next>,
    <"matched"      , matched>,
    <"instruction"  , instruction>,
    <"result"       , result>
  >


// This is a specifier for the active instruction loaded into the machine.

instruction (structure, configurations, operation) ->
  <
    <"structure"    , structure>,     // this key is only to pass down recursively to other evaluations
    <"configurations", configurations>, // this key is only to pass down recursively to other evaluations
    <"operation"    , operation>      // this is an entity that represents the operations to perform
  >


// This is a specifier for a configuration, a pair of a pattern that serves to match the machine's current "structure",
// and when it does, load "instruction".

configuration (pattern, instruction) ->
  <
    <"pattern"  , pattern>,
    <"instruction", instruction>
  >


// ------------------------------------------------------------------------------------------------------------------ //
// OPERATIONS
// ------------------------------------------------------------------------------------------------------------------ //


// This instruction evals an instruction, which has its own set of relations to transition between defined outside the
// universal machine. The nature of those specified relations determines the behavior of the universal machine.

EvalInstruction (structure, configurations, operation) ->
  Eval(
    instruction(structure, configurations, operation)
  )["operation"]


// ------------------------------------------------------------------------------------------------------------------ //
// MACHINE STATES
// ------------------------------------------------------------------------------------------------------------------ //


// This state sets up a new machine by taking in "structure" and "configurations" entities, and setting various defaults.

s0 : universal([], [], [], [*], null, [], [*], [*]) =>
  universal(
    "s0",
    s0["structure"],
    s0["configurations"],
    0,
    s0["configurations"][0],
    null,
    [],
    []
  )


// This state evaluates a matched "instruction".

s1 : universal([], [], [], anynum, [], true, [], [*]) =>
  universal(
    "s1",
    EvalInstruction(
      s1["structure"],
      s1["configurations"],
      s1["instruction"]
    ),
    s1["configurations"],
    0,
    s1["configurations"][0], // begin searching for next match from first element in "configurations"
    null,
    [],
    []
  )


// Stop if there is no next instruction.

s2 : universal([], [], [], anynum, null, [], [], []) =>
  universal(
    "s2",
    [],
    [],
    [],
    null,
    [],
    [],
    s2["structure"]
  )


// Set next "pattern" for matching.

s3_current_configuration :: s3["configurations"][s3["index"]]

s3 : universal([], [], [], anynum, [], null,  [], [*]) =>
  universal(
    "s3",
    s3["structure"],
    s3["configurations"],
    +(s3["index"]),
    s3["configurations"][
      +(s3["index"])
    ],
    Match(
      s3["structure"],
      s3_current_configuration["pattern"]
    ),
    s3_current_configuration["instruction"],
    []
  )


// ------------------------------------------------------------------------------------------------------------------ //
// OPERATION SPECIFIERS & RELATIONS
// ------------------------------------------------------------------------------------------------------------------ //

// When an "operation" entity within and "instruction" takes the following forms, we will evaluate them according to
// the relations defined below. This is what allows us to use the universal to emulate any cogm system.


// Convenience specifier.

op_result(s, result) ->
  instruction(
    s["structure"],
    s["configurations"],
    result
  )


// A "ref" atom is composed with another entity to indicate that it will be evaluated like a symbol that refers to
// another entity in "structure".

ref(entity) -> <<#"ref", entity>>

sRef : instruction([], [], ref([])) =>
  op_result(
    sRef,
    sRef["structure"][
      sRef["operation"][#"ref"]
    ]
  )


// C1 is the compose operation with only one operand, i.e. <op1>

c1(op1) ->
  <
    <"op", "C1">,
    <"op1", op1>
  >

sC1 : instruction([], [], c1([])) =>
  op_result(
    sC1,
    <
      EvalInstruction(sC1["structure"], sC1["configurations"], sC1["operation"]["op1"])
    >
  )


// C2 is the compose operation with two operands

c2(op1, op2) ->
  <
    <"op", "C2">,
    <"op1", op1>,
    <"op2", op2>
  >

sC2 : instruction([], [], c2([], [])) =>
  op_result(
    sC2,
    <
      EvalInstruction(sC2["structure"], sC2["configurations"], sC2["operation"]["op1"]),
      EvalInstruction(sC2["structure"], sC2["configurations"], sC2["operation"]["op2"])
    >
  )


// C3 is the compose operation with three operands

c3(op1, op2, op3) ->
  <
    <"op", "C3">,
    <"op1", op1>,
    <"op2", op2>,
    <"op3", op3>
  >

sC3 : instruction([], [], c3([], [], [])) =>
  op_result(
    sC3,
    <
      EvalInstruction(sC3["structure"], sC3["configurations"], sC3["operation"]["op1"]),
      EvalInstruction(sC3["structure"], sC3["configurations"], sC3["operation"]["op2"]),
      EvalInstruction(sC3["structure"], sC3["configurations"], sC3["operation"]["op3"])
    >
  )


// C4 is the compose operation with three operands

c4(op1, op2, op3, op4) ->
  <
    <"op", "C4">,
    <"op1", op1>,
    <"op2", op2>,
    <"op3", op3>,
    <"op4", op4>
  >

sC4 : instruction([], [], c4([], [], [], [])) =>
  op_result(
    sC4,
    <
      EvalInstruction(sC4["structure"], sC4["configurations"], sC4["operation"]["op1"]),
      EvalInstruction(sC4["structure"], sC4["configurations"], sC4["operation"]["op2"]),
      EvalInstruction(sC4["structure"], sC4["configurations"], sC4["operation"]["op3"]),
      EvalInstruction(sC4["structure"], sC4["configurations"], sC4["operation"]["op4"])
    >
  )


// x specifies the extract operation

x(op1, op2) ->
  <
    <"op", "X">,
    <"op1", op1>,
    <"op2", op2>
  >

sx : instruction([], [], x([], [])) =>
  op_result(
    sx,
    EvalInstruction(sx["structure"], sx["configurations"], sx["operation"]["op1"])[
      EvalInstruction(sx["structure"], sx["configurations"], sx["operation"]["op2"])
    ]
  )


// t specifies the transclude operation

t(op1, op2, op3) ->
  <
    <"op", "T">,
    <"op1", op1>,
    <"op2", op2>,
    <"op3", op3>
  >

st : instruction([], [], t([], [], [])) =>
  op_result(
    st,
    T(
      EvalInstruction(st["structure"], st["configurations"], st["operation"]["op1"]),
      EvalInstruction(st["structure"], st["configurations"], st["operation"]["op2"]),
      EvalInstruction(st["structure"], st["configurations"], st["operation"]["op3"])
    )
  )


// incr specifies the increment operation

incr(op1) ->
  <
    <"op", "+">,
    <"op1", op1>
  >

sincr : instruction([], [], incr([])) =>
  op_result(
    sincr,
    +(
      EvalInstruction(sincr["structure"], sincr["configurations"], sincr["operation"]["op1"])
    )
  )


// decr specifies the decrement operation

decr(op1) ->
  <
    <"op", "-">,
    <"op1", op1>
  >

sdecr : instruction([], [], decr([])) =>
  op_result(
    sdecr,
    -(
      EvalInstruction(sdecr["structure"], sdecr["configurations"], sdecr["operation"]["op1"])
    )
  )


// ------------------------------------------------------------------------------------------------------------------ //
// UNIVERSAL EVALUATION OPERATION
// ------------------------------------------------------------------------------------------------------------------ //


// This is our final operation to execute the universal machine.

Universal(structure, configurations) ->
  Eval(
    universal(
      "begin",
      structure,
      configurations,
      [],
      null,
      [],
      [],
      []
    )
  )
